USE MARCIN_NIERUCHOMOSC;

GO


CREATE FUNCTION getUUID() RETURNS varchar(255) AS
BEGIN
    DECLARE @UUID uniqueidentifier
    SELECT @UUID = uuid
    FROM VIEW_GET_UUID
    RETURN CONVERT(varchar(255), @UUID);
END

GO

CREATE FUNCTION FN_ILE_PROCENT_RABATU(@FROM date, @TO date) RETURNS decimal(2, 2) AS
BEGIN
    IF (DATEDIFF(month, @FROM, @TO) <= 1)
        BEGIN
            RETURN 0.00
        END
    IF (DATEDIFF(month, @FROM, @TO) <= 3)
        BEGIN
            RETURN 0.05
        END
    IF (DATEDIFF(month, @FROM, @TO) > 3 AND DATEDIFF(month, @FROM, @TO) <= 12)
        BEGIN
            RETURN 0.10
        END
    IF (DATEDIFF(month, @FROM, @TO) > 12)
        BEGIN
            RETURN 0.15
        END
    RETURN 0.00;
END

GO
-- TEST FUNKCJI RABAT
PRINT (dbo.FN_ILE_PROCENT_RABATU(convert(datetime, '22.10.2016', 104), convert(datetime, '22.12.2017', 104)))

GO

CREATE FUNCTION FN_POLICZ_NETTO(@ILE_DNI int, @CENA_DOBA decimal(30, 2)) RETURNS decimal(30, 2) AS
BEGIN
    -- NIE MOÅ»E BYC 0 DNI - MINUMUM 1 DOBA
    IF (@ILE_DNI = 0)
        BEGIN
            SET @ILE_DNI = 1;
        END
    DECLARE @NETTO decimal(30, 2);
    SET @NETTO = ((@ILE_DNI * @CENA_DOBA));
    RETURN @NETTO;
END

GO

CREATE FUNCTION FN_POLICZ_BRUTTO(@NETTO decimal(30, 2)) RETURNS decimal(30, 2) AS
BEGIN
    DECLARE @BRUTTO decimal(30, 2);
    SET @BRUTTO = @NETTO * 1.23;
    RETURN @BRUTTO;
END


GO

-- RABAT ODEJMOWANY JEST OD CENNY BRUTTO
CREATE FUNCTION FN_POLICZ_RABAT(@BRUTTO decimal(30, 2), @RABAT decimal(2, 2)) RETURNS decimal(30, 2) AS
BEGIN
    RETURN @BRUTTO * @RABAT;
END

GO

-- TEST KALKULACJI CENY
DECLARE @NETTO decimal(30, 2);
DECLARE @BRUTTO decimal(30, 2);
DECLARE @RABAT decimal(30, 2);

SELECT @NETTO = dbo.FN_POLICZ_NETTO(1, 105.00);
SELECT @NETTO AS NETTO;
SELECT @BRUTTO = dbo.FN_POLICZ_BRUTTO(@NETTO);
SELECT @BRUTTO AS BRUTTO;
SELECT @RABAT = dbo.FN_POLICZ_RABAT(@BRUTTO, 0.13);
SELECT @RABAT AS RABAT;



GO

CREATE PROCEDURE PROC_DODAJ_RACHUNEK(@UUID_NAJEM varchar(255)) AS
BEGIN

    DECLARE @ILE_DNI int;
    DECLARE @RABAT decimal(2, 2);
    DECLARE @CENA_DOBA decimal(30, 2);
    DECLARE @CENA_NETTO decimal(30, 2);
    DECLARE @CENA_BRUTTO decimal(30, 2);
    DECLARE @WARTOSC_RABATU decimal(30, 2);
    DECLARE @WARTOSC_PO_RABACIE decimal(30, 2);

    DECLARE @ID_NAJEM bigint;


    SELECT @ILE_DNI = NI.ILE_DNI, @CENA_DOBA = N.CENA_DOBA_NETTO, @ID_NAJEM = NI.ID_NAJEM, @RABAT = NI.WIELKOSC_RABATU
    FROM NAJEM_INDYWIDUALNY AS NI
             JOIN NIERUCHOMOSC N on NI.ID_NIERUCHOMOSC = N.ID_NIERUCHOMOSC
    WHERE NI.UUID = @UUID_NAJEM;

    SET @CENA_NETTO = dbo.FN_POLICZ_NETTO(@ILE_DNI, @CENA_DOBA);
    SET @CENA_BRUTTO = dbo.FN_POLICZ_BRUTTO(@CENA_NETTO);
    SET @WARTOSC_RABATU = dbo.FN_POLICZ_RABAT(@CENA_BRUTTO, @RABAT);
    SET @WARTOSC_PO_RABACIE = @CENA_BRUTTO - @WARTOSC_RABATU;

    INSERT INTO RACHUNEK(UUID, ID_NAJEM, CENA_NETTO, CENA_BRUTTO, WARTOSC_BRUTTO_PO_RABACIE)
    VALUES (dbo.getUUID(), @ID_NAJEM, @CENA_NETTO, @CENA_BRUTTO, @WARTOSC_PO_RABACIE);

END

GO